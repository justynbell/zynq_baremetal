source [find interface/ftdi/digilent-hs1.cfg]

transport select jtag
adapter speed 10000
reset_config none

source [find target/zynq_7000.cfg]
source ./xilinx-tcl.cfg
source ./ps7_init.tcl

# The Arty Z7 board does not bring out the SRST pin to
# any IOs, it just has a button on the board.
#
# To get to a clean state, I need to halt the part, reinitialize it,
# invalidate the I-Cache and D-cache, and write "0" to the system
# control register which a) disables I-Cache and D-Cache, and causes
# a synchronization event when this register is written to.
proc arty_z7_reset {} {
    echo "Starting Arty Z7 Reset..."

    # Select target
    targets zynq.cpu0

    # Halt the part
    halt

    # Call the Xilinx init stuff
    ps7_init
    ps7_post_config

    # Invalidate entire instruction cache (ICIALLU)
    # 15 (p15): System Control Coprocessor
    # 0  (op1): Primary opcode for cache maintenance
    # 7  (CRn): Register 7: cache operations
    # 5  (CRm): Cache operation registers
    # 0  (op2): ICIALLU register
    # 0  (value): dummy value needed when issuing instructions from OpenOCD
    arm mcr 15 0 7 5 0 0

    # Write SCTLR (System Control Register)
    # 15 (p15): System Control Coprocessor
    # 0  (op1): Primary opcode
    # 1  (CRn): Targets System Control Register (SCTLR)
    # 0  (CRm): System Control Register
    # 0  (op2): SCTLR register
    # 0  (value): Turn everything off
    arm mcr 15 0 1 0 0 0

    echo "done"
}

proc load_flasher {} {
    arty_z7_reset

    adapter speed 10000

    # Unlock SLCR (test removing this)
    mww 0xF8000008 0x0000DF0D

    # Load the flasher application into DDR
    echo "Loading flasher application into DDR"
    load_image C:/dev/sandbox/zynq_baremetal/flasher/flasher.elf

    # Load the BOOT.BIN file into DDR
    echo "Loading BOOT.BIN into DDR"
    load_image C:/dev/sandbox/zynq_baremetal/BOOT.BIN 0x00200000

    # Get the size of the file
    set file_size [file size "C:/dev/sandbox/zynq_baremetal/BOOT.BIN"]
    mww 0x00100004 $file_size
    echo "Binary size: $file_size bytes written to 0x00100004"

    # Trigger the flasher to copy the BOOT.BIN into QSPI flash
    mww 0x00100000 0x01

    reg pc 0x1C000000
    resume

    echo "done"
}