source [find interface/ftdi/digilent-hs1.cfg]

transport select jtag
reset_config none

source [find target/zynq_7000.cfg]
source ./xilinx-tcl.cfg
source ./ps7_init.tcl

zynq.cpu1 configure -defer-examine

# The Arty Z7 board does not bring out the SRST pin to
# any IOs, it just has a button on the board.
#
# To get to a clean state, I need to halt the part, reinitialize it,
# invalidate the I-Cache and D-cache, and write "0" to the system
# control register which a) disables I-Cache and D-Cache, and causes
# a synchronization event when this register is written to.
proc arty_z7_reset {} {
    echo "Starting Arty Z7 Reset..."
    adapter speed 10000

    # Select target
    targets zynq.cpu0

    # Halt the part
    halt

    # Call the Xilinx init stuff
    ps7_init
    ps7_post_config
    # ps7_debug
    sleep 500

    # Invalidate entire instruction cache (ICIALLU)
    # 15 (p15): System Control Coprocessor
    # 0  (op1): Primary opcode for cache maintenance
    # 7  (CRn): Register 7: cache operations
    # 5  (CRm): Cache operation registers
    # 0  (op2): ICIALLU register
    # 0  (value): dummy value needed when issuing instructions from OpenOCD
    arm mcr 15 0 7 5 0 0

    # Write SCTLR (System Control Register)
    # 15 (p15): System Control Coprocessor
    # 0  (op1): Primary opcode
    # 1  (CRn): Targets System Control Register (SCTLR)
    # 0  (CRm): System Control Register
    # 0  (op2): SCTLR register
    # 0  (value): Turn everything off
    arm mcr 15 0 1 0 0 0

    # FreeRTOS turns on interrupts, without disabling them
    # here, the GIC will interrupt the code before it reaches
    # the point in the _boot assembly that sets VBAR
    # (Vector Base Address Register) to our actual _vector_table
    # address defined in the linker script (it's at ~line 189
    # in boot.S). So the interrupt fires, but VBAR is pointing
    # to old code low in the address space, which results in
    # an Undefined Exception handler being called.
    # So disable both the Interrupt Controller Distributor (ICD)
    # and the CPU 0 Distributor

    # ICD disable
    mww 0xF8F01000 0x0
    # CPU Distributor disable
    mww 0xF8F00100 0x0

    echo "done"
}